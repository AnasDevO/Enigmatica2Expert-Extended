# Some tweaks taken from https://github.com/AnodeCathode/TechNodeFirmaCraft

# ---- REGISTER BIOMES ----
# Tweaker.setScriptStage("BIOME_REGISTRY")
# Tweaker.createBiome("red_dead")
# Tweaker.setScriptStage("FINISHED_LOAD")


# ----  ----
# Tweaker.setPlacementStage("BIOME_BLOCKS")
# biome.set("name", "Red Dead")

# biome.registerGenBlockRep("minecraft:stone", "advancedrocketry:hotturf")
# biome.registerGenBlockRep("minecraft:water", "minecraft:lava")
# biome.addActualFillerBlock("advancedrocketry:hotturf")

# /cofh clearblocks LD01 40 200 10 stone biomesoplenty:hard_ice iceandfire:frozen_cobblestone iceandfire:frozen_gravel dirt grass coal_block snow snow_layer packed_ice iceandfire:frozen_dirt iceandfire:frozen_stone chisel:marble2 ice



########################################
# Mercury
########################################
Tweaker.setWorld(101)
biome = forBiomes("biomesoplenty:volcanic_island")



########################################
# Venus
########################################
Tweaker.setWorld(102)
biome = forBiomes("biomesoplenty:volcanic_island")



########################################
# Mars
########################################
Tweaker.setWorld(103)
biome = forBiomes("advancedrocketry:hotdryrock")



########################################
# Europa
########################################
Tweaker.setWorld(106)
biome = forBiomes("minecraft:frozen_ocean")



########################################
# Uranus
########################################
Tweaker.setWorld(109)
biome = forBiomes("biomesoplenty:alps","iceandfire:glacier")



########################################
# Neptune
########################################
Tweaker.setWorld(110)
biome = forBiomes("biomesoplenty:alps_foothills","biomesoplenty:glacier")

Tweaker.setPlacementStage("PRE_DECORATE")
biome.registerGenBlockRep("minecraft:lava", "advancedrocketry:enrichedlavafluid")
biome.registerGenBlockRep("minecraft:water", "tconstruct:molten_obsidian")
Tweaker.setPlacementStage("BIOME_BLOCKS")

br = newBlockReplacement()
b = forBlock("biomesoplenty:hard_ice")
br.set("block", b)
biome.registerGenBlockRep("minecraft:stone", br)
biome.addActualFillerBlock(b)


########################################
# Terra Nova
########################################
Tweaker.setWorld(112)
biome = forBiomes("biomesoplenty:crag","biomesoplenty:quagmire")



########################################
# Novus
########################################
Tweaker.setWorld(113)
biome = forBiomes("biomesoplenty:volcanic_island")

Tweaker.setPlacementStage("PRE_DECORATE")
biome.registerGenBlockRep("minecraft:lava", "advancedrocketry:enrichedlavafluid")
biome.registerGenBlockRep("minecraft:water", "tconstruct:molten_obsidian")
Tweaker.setPlacementStage("BIOME_BLOCKS")

br = newBlockReplacement()
b = forBlock("minecraft:coal_block")
br.set("block", b)
biome.registerGenBlockRep(1000, "minecraft:stone", br)
biome.registerGenBlockRep(100, "minecraft:stone", "minecraft:obsidian")
biome.registerGenBlockRep(10, "minecraft:stone", "minecraft:concrete")

# biome.addActualFillerBlock("minecraft:coal_block")
biome.set("waterColor", 8999212)



########################################
# Stella
########################################
Tweaker.setWorld(114)
biome = forBiomes("biomesoplenty:sacred_springs","biomesoplenty:mystic_grove")



########################################
# Philucka
########################################
Tweaker.setWorld(115)
biome = forBiomes("advancedrocketry:stormland")



########################################
# 118_KELT-2ab
########################################
Tweaker.setWorld(118)
b118 = forBiomes("minecraft:frozen_ocean")
b118.set("waterColor", 9043887)
b118.set("height", 0.01)


########################################
# KELT-3
########################################
Tweaker.setWorld(119)
biome = forBiomes("advancedrocketry:hotdryrock")



########################################
# KELT-4ab
########################################
Tweaker.setWorld(120)
biome = forBiomes("biomesoplenty:alps","iceandfire:glacier","biomesoplenty:alps_foothills","biomesoplenty:glacier")



########################################
# KELT-6a
########################################
Tweaker.setWorld(121)
biome = forBiomes("advancedrocketry:alien_forest","biomesoplenty:ominous_woods")



########################################
# Kepler 0118
########################################
Tweaker.setWorld(122)
biome = forBiomes("biomesoplenty:volcanic_island")

br = newBlockReplacement()
b = forBlock("minecraft:coal_block")
br.set("block", b)
biome.registerGenBlockRep("minecraft:stone", br)
biome.addActualFillerBlock(b)


########################################
# Kepler 0119
########################################
Tweaker.setWorld(123)
biome = forBiomes("advancedrocketry:hotdryrock")
biome.set("height", 4)

blockRepT = newBlockReplacement()
blockT = forBlock("minecraft:stone")
blockT.setProperty("variant", "granite")
blockRepT.set("block", blockT)
blockRepT.set("minY", 36)
blockRepT.set("maxY", 254)
biome.registerGenBlockRep("minecraft:stone", blockRepT)
biome.addActualFillerBlock(blockT)

blockRepM = newBlockReplacement()
blockM = forBlock("biomesoplenty:white_sand")
blockRepM.set("block", blockM)
blockRepM.set("minY", 14)
blockRepM.set("maxY", 35)
biome.registerGenBlockRep("minecraft:stone", blockRepM)

blockRepB = newBlockReplacement()
blockB = forBlock("advancedrocketry:vitrifiedsand")
blockRepB.set("block", blockB)
blockRepB.set("minY", 0)
blockRepB.set("maxY", 13)
biome.registerGenBlockRep("minecraft:stone", blockRepB)

Tweaker.setPlacementStage("PRE_DECORATE")
replacement = newBlockReplacement()
replacement.set("block", "advancedrocketry:enrichedlavafluid")
replacement.set("maxY", 64)
replacement.set("minY", 32)
biome.registerGenBlockRep("minecraft:water", replacement)


import crafttweaker.data.IData;
import crafttweaker.item.IIngredient;
import crafttweaker.item.IItemStack;
import crafttweaker.liquid.ILiquidStack;
import crafttweaker.recipes.IRecipeFunction;
import mods.artisanworktables.builder.RecipeBuilder;
import mods.ctutils.utils.Math.sqrt;
import scripts.craft.grid.Grid;


# Sadly, i didnt find dynamic solution to convert Potion_Name -> EffectName -> EffectId
# This two maps is generated by JavaScript helper functions from ingame command:
# /tellme dump-csv potiontypes
static potNameTag as IData[string] = {
  "cofhcore:absorption"                    : {Id: 22, Amplifier:  0, Duration: 3600},
  "cofhcore:absorption+"                   : {Id: 22, Amplifier:  0, Duration: 9600},
  "cofhcore:absorption2"                   : {Id: 22, Amplifier:  1, Duration: 1800},
  "cofhcore:absorption2+"                  : {Id: 22, Amplifier:  1, Duration: 3200},
  "cofhcore:absorption3"                   : {Id: 22, Amplifier:  2, Duration:  900},
  "cofhcore:absorption3+"                  : {Id: 22, Amplifier:  2, Duration: 2400},
  "cofhcore:absorption4"                   : {Id: 22, Amplifier:  3, Duration:  720},
  "cofhcore:harming3"                      : {Id:  7, Amplifier:  2, Duration:    1},
  "cofhcore:harming4"                      : {Id:  7, Amplifier:  3, Duration:    1},
  "cofhcore:haste"                         : {Id:  3, Amplifier:  0, Duration: 3600},
  "cofhcore:haste+"                        : {Id:  3, Amplifier:  0, Duration: 9600},
  "cofhcore:haste2"                        : {Id:  3, Amplifier:  1, Duration: 1800},
  "cofhcore:haste2+"                       : {Id:  3, Amplifier:  1, Duration: 3200},
  "cofhcore:haste3"                        : {Id:  3, Amplifier:  2, Duration:  900},
  "cofhcore:haste3+"                       : {Id:  3, Amplifier:  2, Duration: 2400},
  "cofhcore:haste4"                        : {Id:  3, Amplifier:  3, Duration:  720},
  "cofhcore:healing3"                      : {Id:  6, Amplifier:  2, Duration:    1},
  "cofhcore:healing4"                      : {Id:  6, Amplifier:  3, Duration:    1},
  "cofhcore:leaping2+"                     : {Id:  8, Amplifier:  1, Duration: 3200},
  "cofhcore:leaping3"                      : {Id:  8, Amplifier:  2, Duration:  900},
  "cofhcore:leaping3+"                     : {Id:  8, Amplifier:  2, Duration: 2400},
  "cofhcore:leaping4"                      : {Id:  8, Amplifier:  3, Duration:  720},
  "cofhcore:levitation"                    : {Id: 25, Amplifier:  0, Duration: 3600},
  "cofhcore:levitation+"                   : {Id: 25, Amplifier:  0, Duration: 9600},
  "cofhcore:luck"                          : {Id: 26, Amplifier:  0, Duration: 3600},
  "cofhcore:luck+"                         : {Id: 26, Amplifier:  0, Duration: 9600},
  "cofhcore:luck2"                         : {Id: 26, Amplifier:  1, Duration: 1800},
  "cofhcore:luck2+"                        : {Id: 26, Amplifier:  1, Duration: 3200},
  "cofhcore:luck3"                         : {Id: 26, Amplifier:  2, Duration:  900},
  "cofhcore:luck3+"                        : {Id: 26, Amplifier:  2, Duration: 2400},
  "cofhcore:luck4"                         : {Id: 26, Amplifier:  3, Duration:  720},
  "cofhcore:poison2+"                      : {Id: 19, Amplifier:  1, Duration:  600},
  "cofhcore:poison3"                       : {Id: 19, Amplifier:  2, Duration:  225},
  "cofhcore:poison3+"                      : {Id: 19, Amplifier:  2, Duration:  450},
  "cofhcore:poison4"                       : {Id: 19, Amplifier:  3, Duration:  180},
  "cofhcore:regeneration2+"                : {Id: 10, Amplifier:  1, Duration:  600},
  "cofhcore:regeneration3"                 : {Id: 10, Amplifier:  2, Duration:  225},
  "cofhcore:regeneration3+"                : {Id: 10, Amplifier:  2, Duration:  450},
  "cofhcore:regeneration4"                 : {Id: 10, Amplifier:  3, Duration:  180},
  "cofhcore:resistance"                    : {Id: 11, Amplifier:  0, Duration: 3600},
  "cofhcore:resistance+"                   : {Id: 11, Amplifier:  0, Duration: 9600},
  "cofhcore:resistance2"                   : {Id: 11, Amplifier:  1, Duration: 1800},
  "cofhcore:resistance2+"                  : {Id: 11, Amplifier:  1, Duration: 3200},
  "cofhcore:resistance3"                   : {Id: 11, Amplifier:  2, Duration:  900},
  "cofhcore:resistance3+"                  : {Id: 11, Amplifier:  2, Duration: 2400},
  "cofhcore:resistance4"                   : {Id: 11, Amplifier:  3, Duration:  720},
  "cofhcore:strength2+"                    : {Id:  5, Amplifier:  1, Duration: 3200},
  "cofhcore:strength3"                     : {Id:  5, Amplifier:  2, Duration:  900},
  "cofhcore:strength3+"                    : {Id:  5, Amplifier:  2, Duration: 2400},
  "cofhcore:strength4"                     : {Id:  5, Amplifier:  3, Duration:  720},
  "cofhcore:swiftness2+"                   : {Id:  1, Amplifier:  1, Duration: 3200},
  "cofhcore:swiftness3"                    : {Id:  1, Amplifier:  2, Duration:  900},
  "cofhcore:swiftness3+"                   : {Id:  1, Amplifier:  2, Duration: 2400},
  "cofhcore:swiftness4"                    : {Id:  1, Amplifier:  3, Duration:  720},
  "cofhcore:unluck"                        : {Id: 27, Amplifier:  0, Duration: 1800},
  "cofhcore:unluck+"                       : {Id: 27, Amplifier:  0, Duration: 4800},
  "cofhcore:unluck2"                       : {Id: 27, Amplifier:  1, Duration:  900},
  "cofhcore:unluck2+"                      : {Id: 27, Amplifier:  1, Duration: 1600},
  "cofhcore:unluck3"                       : {Id: 27, Amplifier:  2, Duration:  450},
  "cofhcore:unluck3+"                      : {Id: 27, Amplifier:  2, Duration: 1200},
  "cofhcore:unluck4"                       : {Id: 27, Amplifier:  3, Duration:  360},
  "cofhcore:wither"                        : {Id: 20, Amplifier:  0, Duration:  900},
  "cofhcore:wither+"                       : {Id: 20, Amplifier:  0, Duration: 1800},
  "cofhcore:wither2"                       : {Id: 20, Amplifier:  1, Duration:  450},
  "cofhcore:wither2+"                      : {Id: 20, Amplifier:  1, Duration:  600},
  "cofhcore:wither3"                       : {Id: 20, Amplifier:  2, Duration:  225},
  "cofhcore:wither3+"                      : {Id: 20, Amplifier:  2, Duration:  450},
  "cofhcore:wither4"                       : {Id: 20, Amplifier:  3, Duration:  180},
  "cyclicmagic:blindness"                  : {Id: 15, Amplifier:  0, Duration: 3600},
  "cyclicmagic:bounce"                     : {Id:102, Amplifier:  0, Duration: 3600},
  "cyclicmagic:butter"                     : {Id:104, Amplifier:  0, Duration: 3600},
  "cyclicmagic:butter2"                    : {Id:104, Amplifier:  1, Duration: 1800},
  "cyclicmagic:ender"                      : {Id: 97, Amplifier:  0, Duration: 3600},
  "cyclicmagic:frostwalker"                : {Id:103, Amplifier:  0, Duration: 3600},
  "cyclicmagic:haste"                      : {Id:  3, Amplifier:  0, Duration: 3600},
  "cyclicmagic:haste2"                     : {Id:  3, Amplifier:  1, Duration: 1800},
  "cyclicmagic:healthboost"                : {Id: 21, Amplifier:  4, Duration: 3600},
  "cyclicmagic:hunger"                     : {Id: 17, Amplifier:  0, Duration: 3600},
  "cyclicmagic:levitation"                 : {Id: 25, Amplifier:  0, Duration: 3600},
  "cyclicmagic:luck"                       : {Id: 26, Amplifier:  0, Duration: 3600},
  "cyclicmagic:magnet"                     : {Id: 96, Amplifier:  0, Duration: 3600},
  "cyclicmagic:resistance"                 : {Id: 11, Amplifier:  0, Duration: 3600},
  "cyclicmagic:resistance2"                : {Id: 11, Amplifier:  1, Duration: 1800},
  "cyclicmagic:saturation"                 : {Id:171, Amplifier:  0, Duration: 3600},
  "cyclicmagic:slowfall"                   : {Id: 99, Amplifier:  0, Duration: 3600},
  "cyclicmagic:snow"                       : {Id:100, Amplifier:  0, Duration: 3600},
  "cyclicmagic:swim"                       : {Id:101, Amplifier:  0, Duration: 3600},
  "cyclicmagic:swim2"                      : {Id:101, Amplifier:  1, Duration: 1800},
  "cyclicmagic:waterwalk"                  : {Id: 98, Amplifier:  0, Duration: 3600},
  "cyclicmagic:wither"                     : {Id: 20, Amplifier:  0, Duration: 3600},
  "enderio:confusion"                      : {Id:  9, Amplifier:  0, Duration:  900},
  "enderio:floating"                       : {Id: 25, Amplifier:  0, Duration:  140},
  "enderio:long_confusion"                 : {Id:  9, Amplifier:  0, Duration: 2400},
  "enderio:long_floating"                  : {Id: 25, Amplifier:  0, Duration:  560},
  "enderio:long_withering"                 : {Id: 20, Amplifier:  0, Duration: 2400},
  "enderio:strong_floating"                : {Id: 25, Amplifier:  2, Duration:  140},
  "enderio:withering"                      : {Id: 20, Amplifier:  0, Duration:  900},
  "extrautils2:xu2.doom"                   : {Id:114, Amplifier:  0, Duration: 1200},
  "extrautils2:xu2.fizzy.lifting"          : {Id:118, Amplifier:  0, Duration:  600},
  "extrautils2:xu2.gravity"                : {Id:115, Amplifier:  0, Duration: 1200},
  "extrautils2:xu2.gravity.long"           : {Id:115, Amplifier:  0, Duration: 9600},
  "extrautils2:xu2.greek.fire"             : {Id:117, Amplifier:  0, Duration: 2400},
  "extrautils2:xu2.love"                   : {Id:120, Amplifier:  0, Duration:  200},
  "extrautils2:xu2.purging"                : {Id:121, Amplifier:  0, Duration:    0},
  "extrautils2:xu2.relapse"                : {Id:119, Amplifier:  0, Duration: 9600},
  "extrautils2:xu2.second.chance"          : {Id:116, Amplifier:  0, Duration: 2400},
  "minecraft:fire_resistance"              : {Id: 12, Amplifier:  0, Duration: 3600},
  "minecraft:harming"                      : {Id:  7, Amplifier:  0, Duration:    1},
  "minecraft:healing"                      : {Id:  6, Amplifier:  0, Duration:    1},
  "minecraft:invisibility"                 : {Id: 14, Amplifier:  0, Duration: 3600},
  "minecraft:leaping"                      : {Id:  8, Amplifier:  0, Duration: 3600},
  "minecraft:long_fire_resistance"         : {Id: 12, Amplifier:  0, Duration: 9600},
  "minecraft:long_invisibility"            : {Id: 14, Amplifier:  0, Duration: 9600},
  "minecraft:long_leaping"                 : {Id:  8, Amplifier:  0, Duration: 9600},
  "minecraft:long_night_vision"            : {Id: 16, Amplifier:  0, Duration: 9600},
  "minecraft:long_poison"                  : {Id: 19, Amplifier:  0, Duration: 1800},
  "minecraft:long_regeneration"            : {Id: 10, Amplifier:  0, Duration: 1800},
  "minecraft:long_slowness"                : {Id:  2, Amplifier:  0, Duration: 4800},
  "minecraft:long_strength"                : {Id:  5, Amplifier:  0, Duration: 9600},
  "minecraft:long_swiftness"               : {Id:  1, Amplifier:  0, Duration: 9600},
  "minecraft:long_water_breathing"         : {Id: 13, Amplifier:  0, Duration: 9600},
  "minecraft:long_weakness"                : {Id: 18, Amplifier:  0, Duration: 4800},
  "minecraft:luck"                         : {Id: 26, Amplifier:  0, Duration: 6000},
  "minecraft:night_vision"                 : {Id: 16, Amplifier:  0, Duration: 3600},
  "minecraft:poison"                       : {Id: 19, Amplifier:  0, Duration:  900},
  "minecraft:regeneration"                 : {Id: 10, Amplifier:  0, Duration:  900},
  "minecraft:slowness"                     : {Id:  2, Amplifier:  0, Duration: 1800},
  "minecraft:strength"                     : {Id:  5, Amplifier:  0, Duration: 3600},
  "minecraft:strong_harming"               : {Id:  7, Amplifier:  1, Duration:    1},
  "minecraft:strong_healing"               : {Id:  6, Amplifier:  1, Duration:    1},
  "minecraft:strong_leaping"               : {Id:  8, Amplifier:  1, Duration: 1800},
  "minecraft:strong_poison"                : {Id: 19, Amplifier:  1, Duration:  432},
  "minecraft:strong_regeneration"          : {Id: 10, Amplifier:  1, Duration:  450},
  "minecraft:strong_strength"              : {Id:  5, Amplifier:  1, Duration: 1800},
  "minecraft:strong_swiftness"             : {Id:  1, Amplifier:  1, Duration: 1800},
  "minecraft:swiftness"                    : {Id:  1, Amplifier:  0, Duration: 3600},
  "minecraft:water_breathing"              : {Id: 13, Amplifier:  0, Duration: 3600},
  "minecraft:weakness"                     : {Id: 18, Amplifier:  0, Duration: 1800},
  "potioncore:absorption"                  : {Id: 22, Amplifier:  0, Duration: 3600},
  "potioncore:antidote"                    : {Id:160, Amplifier:  0, Duration: 3600},
  "potioncore:archery"                     : {Id:165, Amplifier:  0, Duration: 3600},
  "potioncore:bless"                       : {Id:131, Amplifier:  0, Duration:    1},
  "potioncore:blindness"                   : {Id: 15, Amplifier:  0, Duration:  900},
  "potioncore:broken_armor"                : {Id:135, Amplifier:  0, Duration:  900},
  "potioncore:broken_magic_shield"         : {Id:151, Amplifier:  0, Duration:  900},
  "potioncore:burst"                       : {Id:148, Amplifier:  0, Duration:    1},
  "potioncore:chance"                      : {Id:127, Amplifier:  0, Duration:    1},
  "potioncore:climb"                       : {Id:144, Amplifier:  0, Duration: 3600},
  "potioncore:cure"                        : {Id:136, Amplifier:  0, Duration:    1},
  "potioncore:curse"                       : {Id:168, Amplifier:  0, Duration:    1},
  "potioncore:diamond_skin"                : {Id:137, Amplifier:  0, Duration: 3600},
  "potioncore:disorganization"             : {Id:132, Amplifier:  0, Duration:    1},
  "potioncore:dispel"                      : {Id:128, Amplifier:  0, Duration:    1},
  "potioncore:drown"                       : {Id:158, Amplifier:  0, Duration: 3600},
  "potioncore:explode"                     : {Id:122, Amplifier:  0, Duration:    1},
  "potioncore:extension"                   : {Id:167, Amplifier:  0, Duration: 3600},
  "potioncore:fire"                        : {Id:153, Amplifier:  0, Duration:    1},
  "potioncore:flight"                      : {Id:146, Amplifier:  0, Duration: 3600},
  "potioncore:glowing"                     : {Id: 24, Amplifier:  0, Duration: 3600},
  "potioncore:haste"                       : {Id:  3, Amplifier:  0, Duration: 3600},
  "potioncore:health_boost"                : {Id: 21, Amplifier:  0, Duration: 3600},
  "potioncore:hunger"                      : {Id: 17, Amplifier:  0, Duration:  900},
  "potioncore:invert"                      : {Id:125, Amplifier:  0, Duration:    1},
  "potioncore:iron_skin"                   : {Id:152, Amplifier:  0, Duration: 3600},
  "potioncore:klutz"                       : {Id:130, Amplifier:  0, Duration: 3600},
  "potioncore:launch"                      : {Id:129, Amplifier:  0, Duration:    1},
  "potioncore:levitation"                  : {Id: 25, Amplifier:  0, Duration:  600},
  "potioncore:lightning"                   : {Id:123, Amplifier:  0, Duration:    1},
  "potioncore:long_absorption"             : {Id: 22, Amplifier:  0, Duration: 9600},
  "potioncore:long_antidote"               : {Id:160, Amplifier:  0, Duration: 9600},
  "potioncore:long_archery"                : {Id:165, Amplifier:  0, Duration: 9600},
  "potioncore:long_blindness"              : {Id: 15, Amplifier:  0, Duration: 1800},
  "potioncore:long_broken_armor"           : {Id:135, Amplifier:  0, Duration: 1800},
  "potioncore:long_broken_magic_shield"    : {Id:151, Amplifier:  0, Duration: 1800},
  "potioncore:long_climb"                  : {Id:144, Amplifier:  0, Duration: 9600},
  "potioncore:long_diamond_skin"           : {Id:137, Amplifier:  0, Duration: 9600},
  "potioncore:long_drown"                  : {Id:158, Amplifier:  0, Duration: 9600},
  "potioncore:long_extension"              : {Id:167, Amplifier:  0, Duration: 9600},
  "potioncore:long_flight"                 : {Id:146, Amplifier:  0, Duration: 9600},
  "potioncore:long_glowing"                : {Id: 24, Amplifier:  0, Duration: 9600},
  "potioncore:long_haste"                  : {Id:  3, Amplifier:  0, Duration: 9600},
  "potioncore:long_health_boost"           : {Id: 21, Amplifier:  0, Duration: 9600},
  "potioncore:long_hunger"                 : {Id: 17, Amplifier:  0, Duration: 1800},
  "potioncore:long_iron_skin"              : {Id:152, Amplifier:  0, Duration: 9600},
  "potioncore:long_klutz"                  : {Id:130, Amplifier:  0, Duration: 9600},
  "potioncore:long_levitation"             : {Id: 25, Amplifier:  0, Duration: 1200},
  "potioncore:long_magic_focus"            : {Id:138, Amplifier:  0, Duration: 9600},
  "potioncore:long_magic_inhibition"       : {Id:124, Amplifier:  0, Duration: 9600},
  "potioncore:long_magic_shield"           : {Id:133, Amplifier:  0, Duration: 9600},
  "potioncore:long_mining_fatigue"         : {Id:  4, Amplifier:  0, Duration: 9600},
  "potioncore:long_nausea"                 : {Id:  9, Amplifier:  0, Duration: 1800},
  "potioncore:long_perplexity"             : {Id:139, Amplifier:  0, Duration: 9600},
  "potioncore:long_purity"                 : {Id:163, Amplifier:  0, Duration: 9600},
  "potioncore:long_reach"                  : {Id:154, Amplifier:  0, Duration: 9600},
  "potioncore:long_recoil"                 : {Id:156, Amplifier:  0, Duration: 9600},
  "potioncore:long_repair"                 : {Id:145, Amplifier:  0, Duration: 9600},
  "potioncore:long_resistance"             : {Id: 11, Amplifier:  0, Duration: 9600},
  "potioncore:long_revival"                : {Id:134, Amplifier:  0, Duration: 9600},
  "potioncore:long_rust"                   : {Id:147, Amplifier:  0, Duration: 9600},
  "potioncore:long_slow_fall"              : {Id:164, Amplifier:  0, Duration: 9600},
  "potioncore:long_solid_core"             : {Id:141, Amplifier:  0, Duration: 9600},
  "potioncore:long_spin"                   : {Id:166, Amplifier:  0, Duration: 1800},
  "potioncore:long_step_up"                : {Id:157, Amplifier:  0, Duration: 9600},
  "potioncore:long_vulnerable"             : {Id:140, Amplifier:  0, Duration: 9600},
  "potioncore:long_weight"                 : {Id:126, Amplifier:  0, Duration: 9600},
  "potioncore:long_wither"                 : {Id: 20, Amplifier:  0, Duration: 1800},
  "potioncore:love"                        : {Id:150, Amplifier:  0, Duration:    1},
  "potioncore:magic_focus"                 : {Id:138, Amplifier:  0, Duration: 3600},
  "potioncore:magic_inhibition"            : {Id:124, Amplifier:  0, Duration: 3600},
  "potioncore:magic_shield"                : {Id:133, Amplifier:  0, Duration: 3600},
  "potioncore:mining_fatigue"              : {Id:  4, Amplifier:  0, Duration: 3600},
  "potioncore:nausea"                      : {Id:  9, Amplifier:  0, Duration:  900},
  "potioncore:perplexity"                  : {Id:139, Amplifier:  0, Duration: 3600},
  "potioncore:purity"                      : {Id:163, Amplifier:  0, Duration: 3600},
  "potioncore:reach"                       : {Id:154, Amplifier:  0, Duration: 3600},
  "potioncore:recoil"                      : {Id:156, Amplifier:  0, Duration: 3600},
  "potioncore:repair"                      : {Id:145, Amplifier:  0, Duration: 3600},
  "potioncore:resistance"                  : {Id: 11, Amplifier:  0, Duration: 3600},
  "potioncore:revival"                     : {Id:134, Amplifier:  0, Duration: 3600},
  "potioncore:rust"                        : {Id:147, Amplifier:  0, Duration: 3600},
  "potioncore:saturation"                  : {Id: 23, Amplifier:  3, Duration:    1},
  "potioncore:slow_fall"                   : {Id:164, Amplifier:  0, Duration: 3600},
  "potioncore:solid_core"                  : {Id:141, Amplifier:  0, Duration: 3600},
  "potioncore:spin"                        : {Id:166, Amplifier:  0, Duration:  900},
  "potioncore:step_up"                     : {Id:157, Amplifier:  0, Duration: 3600},
  "potioncore:strong_absorption"           : {Id: 22, Amplifier:  1, Duration: 1800},
  "potioncore:strong_archery"              : {Id:165, Amplifier:  1, Duration: 1800},
  "potioncore:strong_bless"                : {Id:131, Amplifier:  1, Duration:    1},
  "potioncore:strong_blindness"            : {Id: 15, Amplifier:  1, Duration:  450},
  "potioncore:strong_broken_armor"         : {Id:135, Amplifier:  1, Duration:  450},
  "potioncore:strong_broken_magic_shield"  : {Id:151, Amplifier:  1, Duration:  450},
  "potioncore:strong_burst"                : {Id:148, Amplifier:  1, Duration:    1},
  "potioncore:strong_chance"               : {Id:127, Amplifier:  1, Duration:    1},
  "potioncore:strong_curse"                : {Id:168, Amplifier:  1, Duration:    1},
  "potioncore:strong_diamond_skin"         : {Id:137, Amplifier:  1, Duration: 1800},
  "potioncore:strong_explode"              : {Id:122, Amplifier:  1, Duration:    1},
  "potioncore:strong_extension"            : {Id:167, Amplifier:  1, Duration: 1800},
  "potioncore:strong_fire"                 : {Id:153, Amplifier:  1, Duration:    1},
  "potioncore:strong_haste"                : {Id:  3, Amplifier:  1, Duration: 1800},
  "potioncore:strong_health_boost"         : {Id: 21, Amplifier:  1, Duration: 1800},
  "potioncore:strong_hunger"               : {Id: 17, Amplifier:  1, Duration:  450},
  "potioncore:strong_iron_skin"            : {Id:152, Amplifier:  1, Duration: 1800},
  "potioncore:strong_klutz"                : {Id:130, Amplifier:  1, Duration: 1800},
  "potioncore:strong_launch"               : {Id:129, Amplifier:  1, Duration:    1},
  "potioncore:strong_levitation"           : {Id: 25, Amplifier:  1, Duration:  300},
  "potioncore:strong_magic_focus"          : {Id:138, Amplifier:  1, Duration: 1800},
  "potioncore:strong_magic_inhibition"     : {Id:124, Amplifier:  1, Duration: 1800},
  "potioncore:strong_magic_shield"         : {Id:133, Amplifier:  1, Duration: 1800},
  "potioncore:strong_mining_fatigue"       : {Id:  4, Amplifier:  1, Duration: 1800},
  "potioncore:strong_reach"                : {Id:154, Amplifier:  1, Duration: 1800},
  "potioncore:strong_recoil"               : {Id:156, Amplifier:  1, Duration: 1800},
  "potioncore:strong_repair"               : {Id:145, Amplifier:  1, Duration: 1800},
  "potioncore:strong_resistance"           : {Id: 11, Amplifier:  1, Duration: 1800},
  "potioncore:strong_revival"              : {Id:134, Amplifier:  1, Duration: 1800},
  "potioncore:strong_rust"                 : {Id:147, Amplifier:  1, Duration: 1800},
  "potioncore:strong_saturation"           : {Id: 23, Amplifier:  7, Duration:    1},
  "potioncore:strong_slow_fall"            : {Id:164, Amplifier:  1, Duration: 1800},
  "potioncore:strong_spin"                 : {Id:166, Amplifier:  1, Duration:  450},
  "potioncore:strong_step_up"              : {Id:157, Amplifier:  1, Duration: 1800},
  "potioncore:strong_teleport"             : {Id:143, Amplifier:  1, Duration:    1},
  "potioncore:strong_vulnerable"           : {Id:140, Amplifier:  1, Duration: 1800},
  "potioncore:strong_weight"               : {Id:126, Amplifier:  1, Duration: 1800},
  "potioncore:strong_wither"               : {Id: 20, Amplifier:  1, Duration:  450},
  "potioncore:teleport"                    : {Id:143, Amplifier:  0, Duration:    1},
  "potioncore:teleport_spawn"              : {Id:155, Amplifier:  0, Duration:  600},
  "potioncore:teleport_surface"            : {Id:161, Amplifier:  0, Duration:    1},
  "potioncore:unluck"                      : {Id: 27, Amplifier:  0, Duration: 6000},
  "potioncore:vulnerable"                  : {Id:140, Amplifier:  0, Duration: 3600},
  "potioncore:weight"                      : {Id:126, Amplifier:  0, Duration: 3600},
  "potioncore:wither"                      : {Id: 20, Amplifier:  0, Duration:  900},
  "quark:danger_sight"                     : {Id: 88, Amplifier:  0, Duration: 3600},
  "quark:haste"                            : {Id:  3, Amplifier:  0, Duration: 3600},
  "quark:long_danger_sight"                : {Id: 88, Amplifier:  0, Duration: 9600},
  "quark:long_haste"                       : {Id:  3, Amplifier:  0, Duration: 9600},
  "quark:long_mining_fatigue"              : {Id:  4, Amplifier:  0, Duration: 9600},
  "quark:long_resilience"                  : {Id: 86, Amplifier:  0, Duration: 9600},
  "quark:long_resistance"                  : {Id: 11, Amplifier:  0, Duration: 9600},
  "quark:mining_fatigue"                   : {Id:  4, Amplifier:  0, Duration: 3600},
  "quark:resilience"                       : {Id: 86, Amplifier:  0, Duration: 3600},
  "quark:resistance"                       : {Id: 11, Amplifier:  0, Duration: 3600},
  "quark:strong_haste"                     : {Id:  3, Amplifier:  1, Duration: 1800},
  "quark:strong_mining_fatigue"            : {Id:  4, Amplifier:  1, Duration: 1800},
  "quark:strong_resilience"                : {Id: 86, Amplifier:  1, Duration: 1800},
  "quark:strong_resistance"                : {Id: 11, Amplifier:  1, Duration: 1800},
  "randomthings:collapse"                  : {Id: 42, Amplifier:  0, Duration:  450},
  "randomthings:long_collapse"             : {Id: 42, Amplifier:  0, Duration:  900},
  "randomthings:strong_collapse"           : {Id: 42, Amplifier:  1, Duration:  300},
} as IData[string];

static elixirNameId as int[string] = {
  "minecraft:speed"                        : 1,
  "minecraft:slowness"                     : 2,
  "minecraft:haste"                        : 3,
  "minecraft:mining_fatigue"               : 4,
  "minecraft:strength"                     : 5,
  "minecraft:instant_health"               : 6,
  "minecraft:instant_damage"               : 7,
  "minecraft:jump_boost"                   : 8,
  "minecraft:nausea"                       : 9,
  "minecraft:regeneration"                 : 10,
  "minecraft:resistance"                   : 11,
  "minecraft:fire_resistance"              : 12,
  "minecraft:water_breathing"              : 13,
  "minecraft:invisibility"                 : 14,
  "minecraft:blindness"                    : 15,
  "minecraft:night_vision"                 : 16,
  "minecraft:hunger"                       : 17,
  "minecraft:weakness"                     : 18,
  "minecraft:poison"                       : 19,
  "minecraft:wither"                       : 20,
  "minecraft:health_boost"                 : 21,
  "minecraft:absorption"                   : 22,
  "minecraft:saturation"                   : 23,
  "minecraft:glowing"                      : 24,
  "minecraft:levitation"                   : 25,
  "minecraft:luck"                         : 26,
  "minecraft:unluck"                       : 27,
  "randomthings:collapse"                  : 42,
  "quark:resilience"                       : 86,
  "quark:danger_sight"                     : 88,
  "cyclicmagic:potion.magnet"              : 96,
  "cyclicmagic:potion.ender"               : 97,
  "cyclicmagic:potion.waterwalk"           : 98,
  "cyclicmagic:potion.slowfall"            : 99,
  "cyclicmagic:potion.snow"                : 100,
  "cyclicmagic:potion.swimspeed"           : 101,
  "cyclicmagic:potion.bounce"              : 102,
  "cyclicmagic:potion.frostwalker"         : 103,
  "cyclicmagic:potion.butter"              : 104,
  "extrautils2:effect.xu2.doom"            : 114,
  "extrautils2:effect.xu2.gravity"         : 115,
  "extrautils2:effect.xu2.second.chance"   : 116,
  "extrautils2:effect.xu2.greek.fire"      : 117,
  "extrautils2:effect.xu2.fizzy.lifting"   : 118,
  "extrautils2:effect.xu2.relapse"         : 119,
  "extrautils2:effect.xu2.love"            : 120,
  "extrautils2:effect.xu2.purging"         : 121,
  "potioncore:explode"                     : 122,
  "potioncore:lightning"                   : 123,
  "potioncore:magic_inhibition"            : 124,
  "potioncore:invert"                      : 125,
  "potioncore:weight"                      : 126,
  "potioncore:chance"                      : 127,
  "potioncore:dispel"                      : 128,
  "potioncore:launch"                      : 129,
  "potioncore:klutz"                       : 130,
  "potioncore:bless"                       : 131,
  "potioncore:disorganization"             : 132,
  "potioncore:magic_shield"                : 133,
  "potioncore:revival"                     : 134,
  "potioncore:broken_armor"                : 135,
  "potioncore:cure"                        : 136,
  "potioncore:diamond_skin"                : 137,
  "potioncore:magic_focus"                 : 138,
  "potioncore:perplexity"                  : 139,
  "potioncore:vulnerable"                  : 140,
  "potioncore:solid_core"                  : 141,
  "potioncore:teleport"                    : 143,
  "potioncore:climb"                       : 144,
  "potioncore:repair"                      : 145,
  "potioncore:flight"                      : 146,
  "potioncore:rust"                        : 147,
  "potioncore:burst"                       : 148,
  "potioncore:love"                        : 150,
  "potioncore:broken_magic_shield"         : 151,
  "potioncore:iron_skin"                   : 152,
  "potioncore:fire"                        : 153,
  "potioncore:reach"                       : 154,
  "potioncore:teleport_spawn"              : 155,
  "potioncore:recoil"                      : 156,
  "potioncore:step_up"                     : 157,
  "potioncore:drown"                       : 158,
  "potioncore:antidote"                    : 160,
  "potioncore:teleport_surface"            : 161,
  "potioncore:purity"                      : 163,
  "potioncore:slow_fall"                   : 164,
  "potioncore:archery"                     : 165,
  "potioncore:spin"                        : 166,
  "potioncore:extension"                   : 167,
  "potioncore:curse"                       : 168,
  "cyclicmagic:potion.saturation"          : 171,
} as int[string];


# Convert Rustic's ElixirEffect entry into CustomPotionEffects entry (for Vanilla or BloodMagic potions)
#  by replacing "Effect" tag to "Id"
static convertElixir as function(IData)IData = function (elixirEffect as IData) as IData {

  var newData = elixirEffect as IData;
  var effect = D(newData).getString("Effect");
  if(!isNull(effect) && elixirNameId has effect) {
    newData = newData - "Effect";
    newData = newData + {Id: elixirNameId[effect] as int} as IData;
  }
  return newData;
};


# ######################################################################
#
# Combining function
#
# ######################################################################

static durationFnc as function(int)int = function (x as int) as int {
  val d = x as double;
  return min(2147483646.0d,  d * 10.0d) as int;
};
static amplifierFnc as function(int)int = function (x as int) as int {
  val d = x as double;
  return min(127, max(1, 50.0d / (d + 10.0d)));
};

# This function evaluates for each of 4 types of crafts:
# 1. Combining vanilla potions into one
# 2. Increase duration on Rustic's Elixirs
# 3. Increase potency of Blood Magic's Flasks
# 4. Combining any type of potions into one, taking maximum of durations and potencys
static potionFunction as IRecipeFunction = function(out, ins, cInfo) {

  # DataList with CustomPotionEffects
  var compoundTags = [] as IData;

  # What kind of recipe is this
  val isLong   = !isNull(D(out.tag).get("PotionLong"));
  val isStrong = !isNull(D(out.tag).get("PotionStrong"));
  val isOMega  = !isNull(D(out.tag).get("PotionOMEGA"));

  # Maximums. Used only for OMega potion
  var maxDuration  as int = 1;
  var maxAmplifier as int = 0;

  # Iterate over all marked items to extract potion Effects
  for m, marked in ins {

    # Try to get elixir data
    val mTag = D(marked.tag);
    var eData = mTag.get("ElixirEffects");

    # Try to get Flask or Vanilla combined data
    if (isNull(eData)) eData = mTag.get("CustomPotionEffects");

    # Try to get vanilla-like potion name
    if (isNull(eData)) {
      eData = mTag.get("Potion");

      # Still No data -> continue next marked item
      if (isNull(eData)) break;

      # Get Potion Effect IData 
      var potEffect = potNameTag[eData.asString()];

      # No potion name or no NBT in this potion (for example Mundane)
      if (isNull(potEffect)) break;

      # Rewrite eData to list
      eData = [potEffect] as IData;
    }

    # Loop over each potion effects in list to amplify it
    # Also calculate maximums for OMega potion
    for i in 0 to eData.length {
      val effect = eData[i];
      var oldDuration  = D(effect).getInt("Duration", 1);
      val oldAmplifier = D(effect).getInt("Amplifier",0);
      var newDuration  = isLong   ? durationFnc(oldDuration)   : oldDuration;
      var newAmplifier = isStrong ? amplifierFnc(oldAmplifier) : oldAmplifier;
      maxDuration  = max(maxDuration,  newDuration);
      maxAmplifier = max(maxAmplifier, newAmplifier);
      compoundTags = compoundTags + [effect + {Duration: newDuration} as IData + {Amplifier: newAmplifier} as IData] as IData;
    }
  } # End of marked iterator

  # We didnt found any actual potions. Craft is futile
  if(compoundTags.length == 0) return null;

  # Merge list to prevent potion effect duplicates
  # Haste II 3:00 + Haste 8:00 => Haste II 8:00
  var mergedList = [] as IData;
  var skipIndexes = [] as int[];
  for i in 0 to compoundTags.length {
    if (!(skipIndexes has i)) {
      var a = compoundTags[i];

      # Swap effect name to effect ID if we convert Elixir to Flask
      if (!isLong) a = convertElixir(a);

      # New effect with all changes
      var newEffect = a;
      
      # Iterate other effects in dataList
      if (i+1 < compoundTags.length) {
        for j in (i+1) to compoundTags.length {
          val b = compoundTags[j];

          # We found effect with same Id
          if (a.Id == b.Id) {
            skipIndexes = skipIndexes + j;
            newEffect = newEffect + {Amplifier: max(a.Amplifier, b.Amplifier)} as IData;
            newEffect = newEffect + { Duration: max(a.Duration , b.Duration )} as IData;
          }
        }
      }

      if(isOMega){
        mergedList = mergedList + [newEffect + {Duration: maxDuration} as IData + {Amplifier: maxAmplifier} as IData] as IData;
      } else {
        mergedList = mergedList + [newEffect] as IData;
      }

    }
  }

  # Clear output lore
  var newTag = out.tag - {display:{Lore:""}} as IData;

  if (out.definition.id == "rustic:elixir") {
    return out.withTag((newTag - "ElixirEffects"      ) + {ElixirEffects: mergedList});
  } else {
    return out.withTag((newTag - "CustomPotionEffects") + {CustomPotionEffects: mergedList});
  }

} as IRecipeFunction;


# Create Artisan's Worktable recipe
function advancedBrew(
    grid as IIngredient[][], # Shaped recipe ingredients
    liquid as ILiquidStack,  # Required liquid
    toolDamage as int,       # How much damage would be dealt to Tool
    output as IItemStack     # Output potion (used in JEI recipe, uotput tag is changed)
  ) { 
  
  var pL = RecipeBuilder.get("chemist");
  pL.setShaped(grid);
  pL.setFluid(liquid);
  // pL.addTool(<tconevo:tool_sceptre:*>, toolDamage);
  pL.addTool(<cyclicmagic:sword_weakness>, toolDamage);
  pL.setExtraOutputOne(<minecraft:splash_potion>.withTag({Potion: "potioncore:dispel"}), 1.0f);
  pL.addOutput(output);
  pL.setRecipeFunction(potionFunction);
  pL.create();

  mods.jei.JEI.addItem(output);
}

# ######################################################################
#
# Potion combining recipe
#
# ######################################################################

val potCombined = <minecraft:potion>.withTag(
  {
    PotionCombined: 1,

    ench:[{lvl:1,id:36}],
    enchantmentColor:327880,
    CustomPotionColor:327691,
    display:{Name:"§dCombo Potion"},

    CustomPotionEffects:[
      {
        Id:1,Amplifier:0,Duration:600
      },{
        Id:2,Amplifier:0,Duration:600
      }
    ]
  }
);
var anyCombined = potCombined | <minecraft:potion:*>;


val vitCrystal = <thaumcraft:crystal_essence>.withTag({Aspects: [{amount: 1, key: "vitium"}]});

advancedBrew(Grid(["pretty",
      "  ◘ ◪ ◘  ",
      "☼ ◩ ◊ ◩ ☼",
      "☼ ▬ ⱋ ▬ ☼",
      "☼ ✝ ▭ ✝ ☼",
      "  ⩉ ⩉ ⩉  "], {
    "◪": <thaumcraft:bottle_taint>,
    "☼": <ore:nuggetNetherStar>,
    "◩": <randomthings:bottleofair>,
    "▬": <randomthings:beans:2>,
    "✝": <ore:gemAmethyst>,
    "▭": <thaumcraft:crucible>.reuse(),
    "⩉": <thaumcraft:nitor_purple>,
    "◘": vitCrystal,
    "◊": anyCombined.marked("m1"),
    "ⱋ": anyCombined.marked("m2"),
  }).shaped(),

  # Liquid, Tool damage, Output
  <liquid:crystal> * 4000,
  100,
  potCombined.withLore(["Combination of 2 potions with same effects and durations"])
);

# ######################################################################
#
# Long Elixir
#
# ######################################################################

val potLong = <rustic:elixir>.withTag(
  {
    PotionLong: 1,

    ench:[{lvl:1,id:36}],
    enchantmentColor:2720000,
    display:{Name:"§aLong Elixir"},

    ElixirEffects:[
      {
        Effect: "minecraft:speed", Duration: (20*60*60*24), Amplifier: 0
      }
    ]
  }
);
var anyLong = <rustic:elixir:*> | potLong;

# generate all possible brews
var anyBrew as IIngredient = <rustic:fluid_bottle>.withTag({Fluid:{FluidName:"wine",Amount:1000,Tag:{Quality:1.0f}}});

for item in itemUtils.getItemsByRegexRegistryName("rustic:fluid_bottle") {
	if (D(item.tag).check("Fluid.Tag.Quality")) { anyBrew |= item.withTag( item.tag + {Fluid:{Tag:{Quality:1.0f}}} ); }
}

advancedBrew(Grid(["pretty",
      "  ◊ ◊ ◊  ",
      "◪ ☼ ◩ ☼ ◪",
      "◪ ▬ ◘ ▬ ◪",
      "◪ ✝ ▭ ✝ ◪",
      "  ⩉ ⩉ ⩉  "], {
    "◪": <ore:nuggetTerrasteel>,
    "☼": <twilightforest:moonworm>,
    "◩": <mysticalagriculture:emerald_essence>,
    "▬": <forestry:propolis:3>,
    "✝": <cookingforblockheads:cow_jar>,
    "▭": <thaumcraft:crucible>.reuse(),
    "⩉": <thaumcraft:nitor_lime>,
    "◘": anyLong.marked("m1"),
    "◊": anyBrew,
  }).shaped(),

  # Liquid, Tool damage, Output
  <liquid:xu_enchanted_metal> * 6000,
  300,
  potLong.withLore(["Increase duration of potion x10"])
);

# ######################################################################
#
# Strong potion
#
# ######################################################################

val potStrong = <bloodmagic:potion_flask>.withTag(
  {
    PotionStrong: 1,

    ench:[{lvl:1,id:36}],
    enchantmentColor:831714,
    CustomPotionColor:27294,
    display:{Name:"§3Strong Potion"},

    CustomPotionEffects: [
      { Id: 1 as byte, Duration: 2400, Amplifier: 5 as byte }
    ]
  }
);

var anyStrong = potStrong | <bloodmagic:potion_flask>.withTag({CustomPotionEffects:[]}, false);

# generate all possible Botania brewFlasks
var anyBrewFlask as IIngredient = null;
for item in loadedMods["botania"].items {
	val defId = item.definition.id;
	if (defId.startsWith("botania:brewflask")) {
		if (isNull(anyBrewFlask)) {
			anyBrewFlask = item;
		} else {
			anyBrewFlask |= item;
		}
	}
}

advancedBrew(Grid(["pretty",
      "  ◪ ⩉ ◪  ",
      "☼ ◩ ◘ ◩ ☼",
      "☼ ⱋ ◊ ⱋ ☼",
      "☼ ▬ ✝ ▬ ☼",
      "  ▭ ▭ ▭  "], {
    "◪": <environmentaltech:aethium_crystal>,
    "☼": <avaritia:resource:2>,
    "◩": <botania:manabottle>,
    "▬": <randomthings:rezstone>,
    "✝": <thaumcraft:crucible>.reuse(),
    "▭": <thaumictinkerer:energetic_nitor>,
    "⩉": Bucket("hot_spring_water"),
    "◘": BucketTag("potion_lingering", {Potion: "cofhcore:haste4"}),
    "◊": anyStrong.marked("m1"),
    "ⱋ": anyBrewFlask,
  }).shaped(),

  # Liquid, Tool damage, Output
  <liquid:blockfluidantimatter> * 8000,
  1000,
  potStrong.withLore(["Combination of all 5 Flask's effects with level X"])
);

# ######################################################################
#
# OMEGA potion
#
# ######################################################################


val potOMEGA = <bloodmagic:potion_flask>.withTag(
  {
    PotionOMEGA: 1,

    ench:[{lvl:1,id:36}],
    enchantmentColor:15326208,
    display:{Name:"§eOMEGA Potion"},

    CustomPotionEffects: [
      { Id: 1 as byte, Duration: 2400, Amplifier: 5 as byte }
    ]
  }
);

advancedBrew(Grid(["pretty",
      "  ◪ ◘ ◪  ",
      "☼ ◩ ◊ ◩ ☼",
      "☼ ▬ ⱋ ▬ ☼",
      "☼ ✝ ▭ ✝ ☼",
      "  ⩉ ⩉ ⩉  "], {
    "◪": <extrautils2:ingredients:16>,
    "☼": <ore:nuggetUltimate>,
    "◩": <deepmoblearning:glitch_heart>,
    "▬": <randomthings:spectreilluminator>,
    "✝": <avaritia:endest_pearl>,
    "▭": <thaumcraft:crucible>.reuse(),
    "⩉": <avaritia:ultimate_stew>,
    "◘": anyStrong.marked("potStrong"),
    "◊": anyLong.marked("potLong"),
    "ⱋ": anyCombined.marked("potCombined"),
  }).shaped(),

  # Liquid, Tool damage, Output
  <liquid:plasma> * 16000,
  3000,
  potOMEGA.withLore(["Combination of §dAll 3 bottles Effects, §aMaximum duration, §3Maximum potency"])
);
